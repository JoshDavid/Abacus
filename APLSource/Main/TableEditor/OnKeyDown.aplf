 OnKeyDown←{
     t←⍵.CurrentTarget
     t.AutoCompleteActive:0
     m←t.CellMode
     k←⍵.Key
     k≡'F10':DisplayTable t
     c←⍵.CtrlKey≡⊂'true'
     s←⍵.ShiftKey≡⊂'true'
     a←⍵.AltKey≡⊂'true'
     u←~c∨s∨a
     (m∊1 2)∧k≡'Escape':A.StopPropagation t⊣Restore ⍵
     i←t.CurCell
     u∧k≡'Enter':t MoveCell i+1 0
     s∧k≡'Enter':t MoveCell i-1 0
     m=2:0                           ⍝ in-cell mode
     u∧k≡'Tab':t MoveCell TabCell t
     s∧k≡'Tab':t MoveCell ¯1 TabCell t
     u∧k≡'ArrowLeft':t MoveCell i-0 1
     u∧k≡'ArrowUp':t MoveCell i-1 0
     u∧k≡'ArrowDown':t ArrowDown 0
     u∧k≡'ArrowRight':t MoveCell i+0 1
     k≡'F2':StartInCellMode ⍵  ⍝
     m=1:0
     (1=≢k)∧(⊃k)∊⎕D,(⊣,⎕C)⎕A:k StartEditMode ⍵
     c∧s∧k≡'Insert':t InsertRow 0
     k≡'F9':t InsertRow 0
     c∧s∧k≡'Delete':t DeleteRow 0
     c∧s∧k≡'ArrowUp':t MoveRowUp 0
     c∧s∧k≡'ArrowDown':t MoveRowDown 0
     0
 }
