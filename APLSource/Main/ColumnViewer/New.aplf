 New←{
     ⍝ ⍺ ←→ Parent
     ⍝ ⍵ ←→ Namespace with Names, Values, etc.
     ⍝ 1 row for headers, 1 row for data
     ⍺←⊣
     ⎕THIS.A←##
     n←≢⍵.Names
     cn←⍵.SortOrder GetColTitles¨⍵.Names
     cnd←{A.New'div'⍵}¨cn
     t←A.NewTable(n⍴⊂'')cnd
     t.ColumnNames←⍵.Names
     t.CurCol←0
     t.class←'columnviewer'
     t.tabindex←0
     t.TextAlign←⍵.TextAlign
     t.Onclick←A.FQP'OnClick'
     t.Onkeydown←A.FQP'OnKeyDown'
     t.PreventDefault←⊂'TABLE' 'keydown'('ArrowLeft' 'ArrowRight')
     h←,A.HeaderCells t
     h.class←'col'∘,¨⍵.TextAlign
     h.Content.id←⊂¨(A.GetNextId 0)∘,¨'-',¨⍕¨⍳≢h

     h[0].class,←' curheader'


     c←A.BodyCells t
     c[0;].class←'col'∘,¨⍵.TextAlign
     c[0;].Content←{A.New'pre'⍵}¨{∊⍵,¨⊂'\u000D'}¨↓⍉⍵.Values
     ⍝c.tabIndex←'0'
     c.Content.id←(⍴c)⍴(A.GetNextId 0)∘,¨'-',¨⍕¨⍳×/⍴c
     c[0;0].class,←' curcol'
     t.autofocus←''
     t
 }
