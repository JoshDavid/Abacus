 OnKeyDown←{
     k←⍵.Key
     k≡'Shift':0
     l←⍵.Target.AutoCompleteElement
     a←0<≢AutoCompleteElements l
     a∧k≡'ArrowRight':SelectItem ⍵
     a∧k≡'Enter':StopPropagation ⍵⊣1 SelectItem ⍵
     a∧k≡'Escape':StopPropagation ⍵⊣1 Clear l
     a∧k≡'ArrowDown':ResetCursor ⍵⊣1 OnArrowUpDown ⍵
     a∧k≡'ArrowUp':ResetCursor ⍵⊣¯1 OnArrowUpDown ⍵
     ~(⊂k)∊,¨l.TriggerKeys:Clear l
     rc st←GetStartText ⍵
     rc:Clear l
     i←l.Items
     b←((≢st)↑¨i)∊⊂st
     ~∨/b:Clear l
     l.(Mask StartText)←b st
     c←##.New¨{'div'⍵}¨b/i ⍝ div or <p> doesnt really matter
     c.class←⊂''
     (0⊃c).class←'active-item'
     _←SetPosition ⍵
     _←l ##.SetProperty'style.font'⍵.Font
     _←l ##.SetInnerHTML c
     t←⍵.CurrentTarget A.GetNearest'table'
     0=t:0
     t.AutoCompleteActive←1
     0
 }
