 GetContent←{
     p←10⌊⍺.DataSize-⍺.WindowIndex+⍺.WindowSize
     i j←⍺.WindowIndex+⍳¨⍺.WindowSize+p
     k←(⍳⍺.FreezeColumns),j
     q←(⍺.RowNumbers/0),k+⍺.RowNumbers
     ⍝ v←i j⌷d.Values  ⍝ For reg mat
     v←(⊂⊂i)⌷¨⍺.Values[k]
     v←⍉↑⍺.FMT[k]{(⍎⍺)⍵}¨v
     v←{⍵:v,⍨ntbs'LCI15'⎕FMT i+1 ⋄ v}⍺.RowNumbers
     n←⍺.ColumnTitles[q]
     t←A.NewTable v n
     h←,A.HeaderCells t
     h.style←⍺.HeaderStyles[q]
     h.class←'sorted-down' '' 'sorted-up'[(⍺.RowNumbers/1),1+⍺.SortDirection[k]]
     h.id←(A.GetNextId 0)∘,¨'-',¨⍕¨⍳≢h
     c←A.BodyCells t
     r←⍺.RowNumbers+⍸⍺.Align[k]∊⊂'right'
     c.class←⊂''
     _←{~⍵:0 ⋄ c[;0].class←⊂'row-number' ⋄ 0}⍺.RowNumbers
     _←{0=≢r:0 ⋄ c[;r].class←'r' ⋄ 0}0


     _←⍺{⍺.WindowIndex[1]≤⍺.FreezeColumns:0

         c[;⍺.ColumnOffset].class←c[;⍺.ColumnOffset].class,¨⊂' more-to-left'
         0

     }0

     c.id←(⍴c)⍴(A.GetNextId 0)∘,¨'-',¨⍕¨⍳×/⍴c
     cc←⍺.TableCell⌷c
     cc.class,←' window-cell'
     t.Content
 }
