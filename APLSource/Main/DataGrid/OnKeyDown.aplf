 OnKeyDown←{
     t←⍵.CurrentTarget
     t.AutoCompleteActive:0
     m←t.CellMode
     k←⍵.Key
     c←⍵.CtrlKey≡⊂'true'
     s←⍵.ShiftKey≡⊂'true'
     a←⍵.AltKey≡⊂'true'
     uc us ua←~c s a
     co←c∧us∧ua
     cs←c∧s∧ua
     ca←c∧a∧us
     u←~c∨s∨a
     k≡'Escape':Escape t
     u∧k≡'Enter':1 0 Move t
     s∧k≡'Enter':¯1 0 Move t
     u∧k≡'Tab':0 1 Move t
     s∧k≡'Tab':0 ¯1 Move t
     m=2:0
     u∧k≡'ArrowRight':0 1 Move t
     u∧k≡'ArrowLeft':0 ¯1 Move t
     u∧k≡'ArrowUp':¯1 0 Move t
     u∧k≡'ArrowDown':1 0 Move t
     co∧k≡'ArrowUp':MoveTop t
     co∧k≡'ArrowDown':MoveBottom t
     co∧k≡'ArrowLeft':0 MoveToColumn t
     co∧k≡'ArrowRight':¯1 MoveToColumn t
     u∧k≡'PageUp':MovePageUp t
     u∧k≡'PageDown':1 MovePageDown t
     u∧k≡'Home':0 MoveToColumn t
     u∧k≡'End':¯1 MoveToColumn t
     co∧k≡'Home':MoveHome t
     co∧k≡'End':MoveEnd t
     k≡'F6':t FreezeColumns 0
     ~t.Write:0
     u∧k≡'F2':StartEditMode t
     m=1:0
     (1=≢k)∧(⊃k)∊⎕D,(⊣,⎕C)⎕A:k StartEnterMode t
     co∧k≡'Insert':InsertRow t
     k≡'F9':InsertRow t
     co∧k≡'Delete':DeleteRow t
     cs∧k≡'Insert':InsertColumn t  ⍝ TODO!
     cs∧k≡'Delete':DeleteColumn t
     co∧k≡'F2':RenameColumn t
     cs∧k≡'ArrowLeft':¯1 MoveColumn t
     cs∧k≡'ArrowRight':1 MoveColumn t
     cs∧k≡'ArrowUp':MoveRowUp t
     cs∧k≡'ArrowDown':MoveRowDown t
     0
 }
