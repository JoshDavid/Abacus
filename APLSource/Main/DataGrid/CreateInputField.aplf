 CreateInputField←{
     ⍝ Inside <td>:
     ⍝ 1. Remove the padding of td
     ⍝ 2. Create a new div (input-cell) to hold <input> element
     ⍝    This div is positioned "relative", so input can be absolute with respect to it.
     ⍝    The input element is positioned "absolute", 100% width.
     ⍝ 3. Create a new div (input-temp) with existing content,
     ⍝    and original td padding to maintain column width.
     ⍝    If this is not done, and the widest content cell is being edited
     ⍝    the column will resize to the next widest content cell.
     ⍝ 4. Set the outerHMTL of <td> so class is changed too.
     _←⎕DL 0.5×⍵.Document.TestMode=2
     t←⍵
     td←t.TableCell⌷A.BodyCells t
     v←td.Content
     ⍺←v
     d←A.New'div'
     d.class←'input-cell'
     i←d A.New'input'
     i.id←'data-grid-input'
     i.OnEnter←''
     i.value←⍺
     i.OriginalValue←v
     i.Oninput←A.FQP'OnInput'
     i.Onblur←A.FQP'OnInputBlur'
     ai←⎕NS''
     ai.AutoCompleteItems←(1⊃t.DataCell)⊃t.AutoCompleteItems
     i.Document←⍵.Document
     _←i ##.AutoComplete.Init ai
     _←td A.SetInnerHTML d
     _←td A.AddClass'in-cell'
     n←⍕≢⍺
     _←i A.ExecuteOnElement'setSelectionRange(',n,',',n,')'
     _←i A.FireEvent'focus'
     i
 }
