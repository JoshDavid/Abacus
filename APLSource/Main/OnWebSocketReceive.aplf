 OnWebSocketReceive←{
     ⍝ ⎕←'OnWebSocketReceive'
     h←⊃⍵
     c←⎕JSON 3⊃⍵ NewForm
     c.Event≡'SJSR':c.result ⎕TPUT c.id
     d←h.Document
     c.Document←d
     c.Event≡'resize':OnWindowResize c
     q←d.StopPropagation
     d.StopPropagation←0
     q:0
     c.CurrentTargetId≡'':0        ⍝ How does this happen?
     c.CurrentTarget←d ElementById c.CurrentTargetId
     c.Target←d{⍵≡'':0 ⋄ 6::0 ⋄ ⍺ ElementById ⍵}c.TargetId
     HandleUnqueuedRequest c:0     ⍝ Running in 0 thread. Is this ok? YES
     ⍝ _←DisplayEventMessage c
     h.LastTID←h.LastTID HandleRequest&c
     ⍝ _←c ⎕TPUT d.QueuedThread
     0
 }
