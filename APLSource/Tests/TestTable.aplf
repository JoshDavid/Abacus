 TestTable←{
     H←##.Main
     New←H.New
     ⍝ Some Data, headers and body values in simple matrix
     h←'Col1' 'Col2' 'Col3'
     c←'charcol' 'numcol' 'charcol'
     d←⍉⍪'One' '1' 'Smith'
     d⍪←'Two' '2' 'Jones'
     d⍪←'Three' '3' 'Blake'
     ⍝ Utils
     newRow←{⍺ New'tr'}
     newHeaderCell←{⍺ New'th'⍵}
     newCell←{New'td'⍵}
     ⍝ Build up table
     t←New'table'
     th←t New'thead'
     tr←th newRow 0
     hc←tr newHeaderCell¨h
     hc.class←c
     tb←t New'tbody'
     cd←newCell¨d
     r←tb newRow¨⍳≢d
     r.Content←↓cd
     r.Content.class←⊂c
     H.DOM2XML t
 }
